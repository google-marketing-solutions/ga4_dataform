config {
    type: "incremental",
    schema: constants.INTERMEDIATE_DATASET,
    description: "Intermediate model for Snapchat Ads campaign stats as cost etc.",
    tags: ["all"],
    bigquery: {
        partitionBy: "Date",
        clusterBy: ["Campaign_name"]
    }
}

pre_operations {
  declare event_date_checkpoint default (
    ${when(incremental(),
      `select max(Date) - interval 3 day from ${self()}`,
      `select cast('${constants.START_DATE}' as date format 'yyyymmdd')`)
    }
  );
  ---
  ${when(incremental(),
    `delete from ${self()} where Date >= event_date_checkpoint`)
  }
}

  select
    Date as date,
    Campaign_name as campaign_name,
    sum(Unique_impressions) as reach,
    sum(Impressions) as impressions,
    sum(Cost) as cost,
    sum(Swipes) as clicks
  from ${ref('stg_snapchat')} as s
  where 
    Date >= event_date_checkpoint
  group by
    1,
    2