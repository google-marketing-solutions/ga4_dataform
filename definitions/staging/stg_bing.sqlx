config {
  type: "incremental",
  schema: constants.STAGING_DATASET,
  description: "Staging model for Bing Ads data containing impressions, clicks and cost. Based on this sheet: https://docs.google.com/spreadsheets/d/1ym6sW6iebnf05_tviqsspr6ffj93N2zFASVucIsYdp4/edit?gid=0#gid=0.",
  tags: ["all"],
  bigquery: {
    partitionBy: "Date",
    clusterBy: ["Campaign_name"]
  }
}

pre_operations {
  declare event_date_checkpoint default (
    ${when(incremental(),
      `select max(Date) - interval 3 day from ${self()}`,
      `select cast('${constants.START_DATE}' as date format 'yyyymmdd')`)
    }
  );
  ---
  ${when(incremental(),
    `delete from ${self()} where Date >= event_date_checkpoint`)
  }
}

with
  source as (
    select
      Date,
      Campaign_name,
      Impressions,
      Cost,
      Clicks
    from ${ref('bing_data')}
    where 
      Date >= event_date_checkpoint
  )
  
select
  *
from
  source
