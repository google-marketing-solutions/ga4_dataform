config {
  type: "incremental",
  schema: constants.STAGING_DATASET,
  description: "Staging model for Google Ads click stats. Basic column casting and value extractions are performed here.",
  tags: ["all", "google_ads"],
  bigquery: {
    partitionBy: "_data_date",
    clusterBy: ["campaign_id", "ad_group_id"]
  }
}

pre_operations {
  declare event_date_checkpoint default (
    ${when(incremental(),
      `select max(_data_date) - interval 3 day from ${self()}`,
      `select cast('${constants.START_DATE}' as date format 'yyyymmdd')`)
    }
  );
  ---
  ${when(incremental(),
    `delete from ${self()} where _data_date >= event_date_checkpoint`)
  }
}

with
  source as (
    select
      _data_date,
      click_view_gclid as gclid,
      campaign_id,
      ad_group_id
    from ${ref('ads_ClickStats_' + constants.GADS_CUSTOMER_ID)}
    where _data_date >= event_date_checkpoint
  )
  
select
  *
from
  source
