config {
    type: "incremental",
    schema: constants.DASHBOARDS_DATASET,
    description: "This table includes all private loan related tracking based on the table applications, to use in analysis or in dashboards. Fetches data from 2024-01-10.",
    tags: ["all"],
    bigquery: {
      partitionBy: "date"
  }
}

pre_operations {
  declare event_date_checkpoint default (
    ${when(incremental(),
      `select max(date) -3 from ${self()}`,
      `select cast('${constants.START_DATE}' as date format 'yyyymmdd')`)
    }
  )
  ---
  ${when(incremental(),
    `delete from ${self()} where date >= event_date_checkpoint`)
  }
}

with applications as (
  select
    *
  from
    ${ref('applications')}
  where
    date >= event_date_checkpoint
    --date = '2024-12-10' /* Simply for testing purposes */
    and product = 'Privatlån'
)

  select
    date,
    session_id,
    device,
    type,
    event_name,
    status,
    REGEXP_EXTRACT(page_location, r'[^/]+//[^/]+(/[^?]*)') AS page_location,
    pages_in_session,
    events_in_session,
    step,
    previous_submit_location,
    source_medium,
    channel_group,
    campaign,
    event_params.main_applicant as main_applicant,
    event_params.co_applicant as co_applicant,
    event_params.button_text as button_text,
    REGEXP_EXTRACT(event_params.destination_url, r'[^?]+') AS destination_url,
    case
      when regexp_contains(event_name, 'start') then 'Start'
      when regexp_contains(step, '1') then 'Steg 1 - Frågor om lånet'
      when regexp_contains(step, '2') then 'Steg 2 - Uppgifter om dig'
      when regexp_contains(step, 'Uppgifter om din medsökande') then 'Steg 2.5 - Uppgifter om din medsökande'
      when regexp_contains(step, 'Ekonomi & levnadskostnader') then 'Steg 3 - Ekonomi & levnadskostnader'
      when regexp_contains(step, 'Syfte med lånet') then 'Steg 4 - Syfte med lånet'
      when regexp_contains(step, 'Godkänn villkor & ansök') then 'Steg 5 - Godkänn villkor & ansök'
      when regexp_contains(step, 'Submit') then 'Submit'
      when event_name = 'blanco_application_sign_click' then 'Sign click'
      when event_name = 'blanco_application_signed' then 'Sign'
      when event_name = 'blanco_autogiro_sign_click' then 'Autogiro sign click'
      when event_name = 'blanco_loan_protection_cta_click' then 'CTA click'
      when event_name = 'blanco_loan_protection_sign' then 'Sign'
    else '99' end as sort,
    sum(event_value) as event_value
  from
    applications
  where 
  date >= '2024-01-10'
  group by
    date,
    session_id,
    device,
    type,
    event_name,
    status,
    page_location,
    pages_in_session,
    events_in_session,
    step,
    previous_submit_location,
    source_medium,
    channel_group,
    campaign,
    main_applicant,
    co_applicant,
    button_text,
    destination_url