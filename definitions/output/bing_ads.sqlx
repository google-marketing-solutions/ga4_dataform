config {
    type: "incremental",
    schema: constants.OUTPUT_DATASET,
    description: "Bing Ads data containing impressions, clicks and cost. Based on this sheet: https://docs.google.com/spreadsheets/d/1ym6sW6iebnf05_tviqsspr6ffj93N2zFASVucIsYdp4/edit?gid=0#gid=0.",
    tags: ["all"],
    bigquery: {
      partitionBy: "Date"
  }
}

pre_operations {
  declare event_date_checkpoint default (
    ${when(incremental(),
      `select max(date) -3 from ${self()}`,
      `select cast('${constants.START_DATE}' as date format 'yyyymmdd')`)
    }
  )
  ---
  ${when(incremental(),
    `delete from ${self()} where date >= event_date_checkpoint`)
  }
}

  select
    date,
    'Bing' as platform,
    campaign_name,
    case
      when regexp_contains(campaign_name, 'traffic') then 'Performance'
      when regexp_contains(campaign_name, 'brand') then 'Brand'
    else null end as purpose,
    case
      when regexp_contains(campaign_name, 'privatlan|Privatlån') then 'Privatlån'
      when regexp_contains(campaign_name, 'bolan|Bolån') then 'Bolån'
      when regexp_contains(campaign_name, 'kort|Kort') then 'Kort'
      when regexp_contains(campaign_name, 'spara|Spara') then 'Spara'
    else null end as product,
    case
      when regexp_contains(campaign_name, 'flytta') then 'Flytta bolån'
      when regexp_contains(campaign_name, 'nytt') then 'Nytt bolån'
      when regexp_contains(campaign_name, 'commitment') then 'Lånelöfte'
      when regexp_contains(campaign_name, 'raise') then 'Utöka bolån'
    else null end as type,
    0 as reach,
    sum(impressions) as impressions,
    sum(clicks) as clicks,
    sum(cost) as cost
  from ${ref('int_bing_ads_metrics')}
  where 
    date >= event_date_checkpoint
  group by
    date,
    platform,
    campaign_name